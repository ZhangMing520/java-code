plugins {
    id 'com.zeroc.gradle.ice-builder.slice' version '1.4.7' apply false
}

subprojects {

    group 'org.example'
    version '1.0-SNAPSHOT'

    // Apply Java and Ice Builder plug-ins to all sub-projects
    apply plugin: 'java'
    apply plugin: 'com.zeroc.gradle.ice-builder.slice'

    sourceCompatibility = 1.8

    // Both Client and Server projects share the Printer.ice slice definitions
    slice {
        java {
            files = [file("../Printer.ice")]
        }
    }

    // Use Ice JAR files from maven central repository
    repositories {
        mavenLocal()
        maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
        mavenCentral()
    }

    // Both Client and Server depend only on Ice JAR
    dependencies {
        implementation 'com.zeroc:ice:3.7.2'
    }

    // Create a JAR file with the appropriate Main-Class and Class-Path attributes
    jar {
        manifest {
            attributes(
                    "Main-Class": project.name.capitalize(),
                    "Class-Path": configurations.runtime.resolve().collect { it.toURI() }.join(' ')
            )
        }
    }

}
